"""
PartType0: Gas
PartType1: Grains(Epstein/Stokes and charged particles)
PartType2: Grains(Epstein/Stokes)
PartType3: Super-particles
PartType4: Stars
PartType5: Black holes or Collisioness particles
"""

"""
    read_gizmo(filename :: String)
Reading the HDF5-format dumpfiles generated by GIZMO to a list of `PhantomRevealerDataFrame`

# Parameters 
- `filename::String`: Name of the file to be loaded.

# Returns
- `PhantomRevealerDataFrame` or `Vector`: PhantomRevealerDataFrame or Array of PhantomRevealerDataFrames
"""
function read_gizmo(filename::String) :: Vector{PhantomRevealerDataFrame}
    Header = h5readattr(filename, "Header")
    Header["udist"] = 3.085678e21    # kpc --> cm
    Header["umass"] = 1.989e43       # 10^10 MâŠ™ --> g
    Header["utime"] = 3.0842208e16   # 0.978 Gyr --> s
    Header["uv"] = 1.0e5             # km/s --> cm/s
    Header["umagfd"] = 1.0           # Gauss --> Gauss

    prdf_list = Vector{PhantomRevealerDataFrame}()

    h5open(filename, "r") do f
        group_keys = collect(keys(f))
        ignore_column = Set(["ParticleIDs", "Coordinates", "Velocities", "Masses", "SmoothingLength", "TimeStep", "Density"])

        for group_key in group_keys
            if group_key == "Header"
                continue
            end

            SpecificHeader = copy(Header)
            SpecificHeader["PartType"] = group_key

            Particles = f[group_key]
            dict_keys = collect(keys(Particles))

            ParticleIDs = read(Particles, "ParticleIDs")
            coordinate = read(Particles, "Coordinates")
            velocity = read(Particles, "Velocities")
            Masses = read(Particles, "Masses")
            dt = read(Particles, "TimeStep")
            x, y, z = coordinate[1, :], coordinate[2, :], coordinate[3, :]
            vx, vy, vz = velocity[1, :], velocity[2, :], velocity[3, :]

            df = DataFrame(
                ParticleIDs = ParticleIDs,
                x = x, y = y, z = z,
                vx = vx, vy = vy, vz = vz,
                m = Masses, dt = dt
            )

            if group_key == "PartType0"
                h = read(Particles, "SmoothingLength")
                rho = read(Particles, "Density")
                df[!,"h"] = h
                df[!,"rho"] = rho
            end

            for dict_key in dict_keys
                if dict_key in ignore_column
                    continue
                end

                data = read(Particles, dict_key)
                size_data = size(data)

                if length(size_data) == 1
                    df[!, dict_key] = data
                elseif length(size_data) == 2
                    num_rows = size_data[1]
                    dkeys = [dict_key * "_$n" for n in 1:num_rows]
                    for k in 1:num_rows
                        df[!, dkeys[k]] = data[k, :]
                    end
                end
            end

            prdf = PhantomRevealerDataFrame(df, SpecificHeader)
            push!(prdf_list, prdf)
        end
    end

    return prdf_list
end